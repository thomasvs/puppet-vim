define vim($home_dir) {
  $user = $name

  include wget

  include vim::install

  validate_string($user)
  validate_absolute_path($home_dir)

  file { [
    "${home_dir}/.vim",
    "${home_dir}/.vim/autoload",
    "${home_dir}/.vim/bundle",
    ] :
    ensure => 'directory',
    owner  => $user,
  }

  file { "${home_dir}/.vimrc.local" :
    owner   => $user,
    replace => false,
    content => "\"Add here your custom options for vim, puppet will not override them\n",
  }

  wget::fetch { "DownloadPathogen-${user}":
    source      => 'https://raw.github.com/tpope/vim-pathogen/master/autoload/pathogen.vim',
    destination => "${home_dir}/.vim/autoload/pathogen.vim",
    verbose     => true
  }

  file { "${home_dir}/.vim/autoload/pathogen.vim":
    owner => $user
  }

  concat { "${user}:vimrc":
    path  => "${home_dir}/.vimrc",
    owner => $user,
    group => 'root',
    mode  => '0664',
  }

 # Concat::Fragment {
 #   target  => "${user}:vimrc",
 # }

  concat::fragment { "${user}:rc-header":
    target  => "${user}:vimrc",
    content => "\" generated by Puppet module vim\n\n",
    order   => '05',
  }

  vim::rc { "${user}:vimrc-pathogen":
    content => "execute pathogen#infect()\ncall pathogen#helptags()",
  }

  vim::rc { "${user}:vimrc-local":
    content => "if filereadable(glob(\"~/.vimrc.local\"))\n\tsource ~/.vimrc.local\nendif",
  }

  vim::rc { "${user}:syntax on": }
  vim::rc { "${user}:filetype plugin indent on": }
  vim::rc { "${user}:highlight comment ctermfg=darkgray": }
  vim::rc { "${user}::set bg=dark": }

  Package['vim']
  -> File[
    "${home_dir}/.vim",
    "${home_dir}/.vim/autoload",
    "${home_dir}/.vim/bundle"
  ]
  -> Wget::Fetch["DownloadPathogen-${user}"]
  -> File["${home_dir}/.vim/autoload/pathogen.vim"]
  -> Concat["${user}:vimrc"]

}
